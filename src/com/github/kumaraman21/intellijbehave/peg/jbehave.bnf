{
  parserClass="com.github.kumaraman21.intellijbehave.parser.StoryPegParser"
  parserUtilClass="com.github.kumaraman21.intellijbehave.peg.StoryPegParserUtil"
  elementTypeHolderClass="com.github.kumaraman21.intellijbehave.parser.IStoryPegElementType"
  elementTypePrefix="STORY_"
  elementTypeClass="com.github.kumaraman21.intellijbehave.highlighter.StoryPegElementType"
  tokenTypeClass="com.github.kumaraman21.intellijbehave.highlighter.StoryPegTokenType"
  psiClassPrefix="Story"
  psiImplClassSuffix="Impl"
  psiPackage="com.github.kumaraman21.intellijbehave.psi"
  psiImplPackage="com.github.kumaraman21.intellijbehave.psi.impl"

  tokens=[
    TOKEN_META = 'Meta:'
    TOKEN_NARRATIVE = 'Narrative:'
    TOKEN_SCENARIO = 'regexp:Scenario:'
    TOKEN_GIVEN_STORIES= 'GivenStories:'
    TOKEN_GIVEN = 'Given'
    TOKEN_WHEN='When'
    TOKEN_THEN='Then'
    TOKEN_AND='And'
    TOKEN_EXAMPLES='Examples:'
    TOKEN_COMMENT="!--"
    TOKEN_COLON="regexp::"
    TOKEN_AT="@"
    TOKEN_COMMA=","
    //TOKEN_DOT="."
    TOKEN_DBRACKET_OPEN = "<<"
    TOKEN_DBRACKET_CLOSE = ">>"
    TOKEN_BRACKET_OPEN = "<"
    TOKEN_BRACKET_CLOSE = ">"
    TOKEN_LIFECYCLE="Lifecycle:"
    TOKEN_BEFORE="Before:"
    TOKEN_AFTER="After:"
    TOKEN_PIPE='|'

    TOKEN_PATH = 'regexp:[a-zA-Z_0-9._@-]+(/[a-zA-Z_0-9._@-]+)+'
    TOKEN_IP = "regexp:[0-9]+\.[0-9]+\.[0-9]+\.[0-9]+"
    //TOKEN_PUNCT = 'regexp:[\p{Punct}&&[^|@:]]'
    TOKEN_PUNCT = "regexp:[!\"#$%&'()*+,-./;=?\[\\\]\^_`{}~]"
    TOKEN_WORD = 'regexp:[a-zA-Z_0-9]+'
    //TOKEN_WORD_EX = 'regexp:[\w]+\p{Blank}*:'
    TOKEN_NEWLINE='regexp:(\r|\n|\r\n)+'
    TOKEN_SPACE='regexp:[ \t\f]'
  ]
  extends("StoryPath")=" com.github.kumaraman21.intellijbehave.peg.PegStoryPath"
  extends("StepGiven|StepThen|StepWhen|StepAnd")=" com.github.kumaraman21.intellijbehave.parser.JBehaveStep"
  extends("ScenarioTitle|TableCell")=" com.github.kumaraman21.intellijbehave.peg.JBehaveRule"
}
root ::= !<<eof>> (Story|Table) {pin=1}
Story ::= Description? MetaStatement? Narrative? GivenStories? Lifecycle? Scenario+
Description ::= MultiTextLine
MetaStatement ::= TOKEN_META (Space MetaElement)? Newline (MetaElement (TOKEN_NEWLINE|<<eof>>))* {pin=1 }
MetaElement::= TOKEN_AT MetaKey Space MetaValue
MetaKey::= TOKEN_AT TOKEN_WORD
MetaValue::=TOKEN_WORD

Narrative ::= TOKEN_NARRATIVE Newline? NarrativeText {pin=1 }
NarrativeText ::= MultiTextLine

GivenStories::= TOKEN_GIVEN_STORIES SpaceStar StoryPaths Newline {pin=1 }
StoryPaths ::= StoryPath (TOKEN_COMMA StoryPath)*
StoryPath ::= TOKEN_PATH

Lifecycle::= TOKEN_LIFECYCLE LifecycleBefore? LifecycleAfter?
LifecycleBefore::= TOKEN_BEFORE Step+ {pin=2}
LifecycleAfter::= TOKEN_AFTER Step+ {pin=2}

Scenario ::= TOKEN_SCENARIO SpaceStar (ScenarioTitle Newline)? MetaStatement? GivenStories? Step+ Examples? {pin=1}
ScenarioTitle ::= TOKEN_WORD ((Punct|Space) TOKEN_WORD)* | TOKEN_PATH

Step ::=  (StepWhen|StepGiven|StepThen|StepAnd|StepComment)
StepWhen ::=  TOKEN_WHEN SpaceStar StepArgument {pin=1 recoverWhile=RecoverStep}
StepGiven ::=  TOKEN_GIVEN SpaceStar StepArgument {pin=1 recoverWhile=RecoverStep}
StepThen ::=  TOKEN_THEN SpaceStar StepArgument {pin=1 recoverWhile=RecoverStep}
StepAnd ::=  TOKEN_AND SpaceStar StepArgument {pin=1 recoverWhile=RecoverStep}

StepComment ::= TOKEN_COMMENT (TOKEN_WHEN|TOKEN_THEN|TOKEN_GIVEN|TOKEN_AND|StoryPath|LineEx|Line)* Newline

StepArgument ::=  StepLine (SpaceStar StepPostParameter)? Newline?
StepLine ::= Word (SpacePlus Word|PUNCT)* TOKEN_COLON?
StepPostParameter ::=  Newline Table| StoryPath

RecoverStep ::= !(TOKEN_GIVEN|TOKEN_AND|TOKEN_THEN|TOKEN_WHEN|TOKEN_SCENARIO|TOKEN_COMMENT|TOKEN_GIVEN_STORIES|TOKEN_META|TOKEN_EXAMPLES)

Examples ::= TOKEN_EXAMPLES (Table|StoryPath) {pin=1}

Table ::= TableRow+
TableRow ::= TOKEN_PIPE (SpaceStar TableCell SpaceStar | TableCellEmpty) TOKEN_PIPE ((SpaceStar TableCell SpaceStar | TableCellEmpty) TOKEN_PIPE)* Newline
TableCell ::= (Word|Punct)+ (SpacePlus (Word|Punct)+)*
TableCellEmpty ::= SpaceStar

Line ::= (Word|Space|Punct)+
LineEx ::= (Word|Space|Punct)+ TOKEN_COLON
MultiTextLine ::= ((Word|Space|TOKEN_COLON|Punct)+ Newline)+

Word ::= IpAddress|Uri|Alnum|Inject|UserInject
UserInject ::= TOKEN_DBRACKET_OPEN (IpAddress|Uri|Alnum|Space|TOKEN_PIPE|Punct)+ TOKEN_DBRACKET_CLOSE
Inject ::= TOKEN_BRACKET_OPEN (IpAddress|Uri|Alnum|Space|TOKEN_PIPE|Punct)+ TOKEN_BRACKET_CLOSE
Uri::= TOKEN_WORD TOKEN_COLON TOKEN_WORD TOKEN_AT IpAddress (TOKEN_COLON TOKEN_WORD)? {pin=5}
IpAddress ::= TOKEN_IP
Alnum::=TOKEN_WORD

private Newline ::= TOKEN_NEWLINE|<<eof>>
private Space ::= TOKEN_SPACE
private SpacePlus ::= TOKEN_SPACE+
private SpaceStar ::= TOKEN_SPACE*
private Punct ::= TOKEN_PUNCT | TOKEN_BRACKET_CLOSE|TOKEN_BRACKET_OPEN|TOKEN_COMMA|TOKEN_COLON

